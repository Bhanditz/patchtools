buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.1-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'

sourceCompatibility = 1.8
version = '0.4-SNAPSHOT'
group = "uk.co.thinkofdeath.patchtools"

mainClassName = "uk.co.thinkofdeath.patchtools.main.Patch"

repositories {
    mavenCentral()
    maven {
        url "http://repo.thinkofdeath.co.uk/content/groups/public/"
    }
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
}

jar {
    archiveName = "PatchTools.jar"
    manifest.attributes("Main-Class": mainClassName)
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task normalJar(type: Jar) {
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives normalJar
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '0.10.195'
    compile group: 'org.ow2.asm', name: 'asm-debug-all', version: '5.0.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://repo.thinkofdeath.co.uk/content/repositories/snapshots/") {
                authentication(userName: thinkUsername, password: thinkPassword)
            }

            pom.project {
                name "Patch Tools"
                packaging "jar"
                description ""
                url "https://github.com/thinkofdeath/patchtools"

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }

                repositories {
                    repository {
                        id "sonatype"
                        url "http://oss.sonatype.org/content/repositories/snapshots"
                    }
                }

                developers {
                    developer {
                        id "thinkofdeath"
                        name "Thinkofdeath"
                    }
                }
            }
        }
    }
}